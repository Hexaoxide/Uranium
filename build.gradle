buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
    }
}

plugins {
    id 'net.minecrell.plugin-yml.bukkit' version '0.4.0'
}

apply plugin: 'java'

group = pluginGroup
version = pluginVersion

sourceCompatibility = '16'
targetCompatibility = '16'

apply plugin: "com.github.johnrengelman.shadow"

build.dependsOn shadowJar

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    minimize()
    archiveClassifier.set("")
    archiveFileName.set(project.name + ".jar")
}

jar {
    archiveFileName.set(project.name + ".jar")
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "net.draycia.uranium.libs" // Default value is "shadow"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

repositories {
    mavenCentral()
    maven {url = 'https://papermc.io/repo/repository/maven-public/'}
    maven {url = 'https://oss.sonatype.org/content/groups/public/'}
    maven {url = 'https://repo.spongepowered.org/maven'}
    maven {url = 'https://repo.aikar.co/content/groups/aikar/'}
    maven {url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'}
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.5'
    implementation 'net.kyori:adventure-text-minimessage:4.2.0-SNAPSHOT'
    implementation 'net.kyori:adventure-text-serializer-plain:4.8.1'
    implementation 'org.spongepowered:configurate-yaml:3.7'
    implementation "co.aikar:acf-bukkit:0.5.0-SNAPSHOT"
    implementation "com.zaxxer:HikariCP:2.4.1"
    implementation "co.aikar:idb-bukkit:1.0.0-SNAPSHOT"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    duplicatesStrategy = 'include'
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

bukkit {
    main = 'net.draycia.uranium.Uranium'
    description = "Adds various minigames to chat."
    version = project.version as String
    apiVersion = '1.13'
    load = 'STARTUP'
    authors = ['MTM123', 'Vicarious']
    softDepend = ['Vault', "PlaceholderAPI"]
}