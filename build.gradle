buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
    }
}

plugins {
    id 'net.minecrell.plugin-yml.bukkit' version '0.4.0'
    id 'xyz.jpenilla.run-paper' version '1.0.4'
}

apply plugin: 'java'

group = pluginGroup
version = pluginVersion

sourceCompatibility = '16'
targetCompatibility = '16'

apply plugin: "com.github.johnrengelman.shadow"

build.dependsOn shadowJar

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    minimize()
    relocate 'net.kyori.adventure.text.minimessage', 'net.draycia.uranium.libs.net.kyori.adventure.text.minimessage'
    relocate 'net.kyori.adventure.text.serializer.plain', 'net.draycia.uranium.libs.net.kyori.adventure.serializer.plain'
    relocate 'com.zaxxer', 'net.draycia.uranium.libs.com.zaxxer'
    relocate 'co.aikar', 'net.draycia.uranium.libs.co.aikar'
    relocate 'org.checkerframework', 'net.draycia.uranium.libs.org.checkerframework'
    relocate 'org.slf4j', 'net.draycia.uranium.libs.org.slf4j'
    relocate 'org.codehaus', 'net.draycia.uranium.libs.org.codehaus'
    relocate 'org.yaml', 'net.draycia.uranium.libs.org.yaml'
    relocate 'org.spongepowered.configurate', 'net.draycia.uranium.libs.org.spongepowered.configurate'
    relocate 'io.leangen.geantyref', 'net.draycia.uranium.libs.io.leangen.geantyref'
    archiveClassifier.set("")
    archiveFileName.set(project.name + ".jar")
}

jar {
    archiveFileName.set(project.name + ".jar")
}

repositories {
    mavenCentral()
    maven {url = 'https://papermc.io/repo/repository/maven-public/'}
    maven {url = 'https://oss.sonatype.org/content/groups/public/'}
    maven {url = 'https://repo.spongepowered.org/maven'}
    maven {url = 'https://repo.aikar.co/content/groups/aikar/'}
    maven {url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'}
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.5'
    implementation('net.kyori:adventure-text-minimessage:4.2.0-SNAPSHOT') {
        exclude group: 'net.kyori', module: 'adventure-api'
    }
    implementation('net.kyori:adventure-text-serializer-plain:4.8.1') {
        exclude group: 'net.kyori', module: 'adventure-api'
    }
    implementation 'org.spongepowered:configurate-yaml:4.1.2'
    implementation "co.aikar:acf-bukkit:0.5.0-SNAPSHOT"
    implementation "com.zaxxer:HikariCP:2.4.1"
    implementation "co.aikar:idb-bukkit:1.0.0-SNAPSHOT"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    duplicatesStrategy = 'include'
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

bukkit {
    main = 'net.draycia.uranium.Uranium'
    description = "Adds various minigames to chat."
    version = project.version as String
    apiVersion = '1.13'
    load = 'STARTUP'
    authors = ['MTM123', 'Vicarious']
    softDepend = ['Vault', "PlaceholderAPI"]
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.17.1")
    }
}